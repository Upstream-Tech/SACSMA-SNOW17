C MEMBER PACK19
C  (from old member FCPACK19)
C
      SUBROUTINE PACK19(NDT,TA,PX,PCTS,RSL,OWE,OSC,PGM,RM,TWE,
C                             LAST UPDATE: 06/22/95.14:05:09 BY $WC30EA
C
     1            COVER,CWE,CAESC,IFUT,IDT,IDN,IMN,IYR,
C     PREVIOUS TEMPERATURE
     1            TPREV,
C     SNOWFALL (SXFALL),SNOW DENSITY (DS)
     1            SXFALL,DS,
C     SNOW17 PARAMETERS
     &            ALAT,SCF,MFMAX,MFMIN,UADJ,SI,NMF,TIPM,MBASE,
     &            PXTEMP,PLWHC,DAYGM,ADC,
C     SNOW17 PARAMETERS (DERIVED)
     &            PA,LMFV,SMFV,LAEC,NPTAE,AE)
C.......................................
C     THIS SUBROUTINE EXECUTES THE 'SNOW-17 ' OPERATIONAL FOR ONE
C        COMPUTATIONAL PERIOD.
C.......................................
C     SUBROUTINE INITIALLY WRITTEN BY...
C        ERIC ANDERSON - HRL   MAY 1980

C        UPDATED 4/15/00 BY V. KOREN TO ADD SNOW DEPTH CALCULATIONS
C.......................................
      REAL MFMAX,MFMIN,NMF,LIQW,NEGHS,MBASE,MELT,LIQWMX,MFC
      DIMENSION ADC(12),SMFV(12),AE(2,14)
      DIMENSION PX(NDT),PCTS(NDT),RM(NDT)

C     COMMON BLOCKS
      COMMON/FSNWUP/IUPWE,IUPSC
      COMMON/SNCO19/WE,NEGHS,LIQW,TINDEX,ACCMAX,SB,SBAESC,SBWS,
     1   STORGE,AEADJ,NEXLAG,EXLAG(7),SNDPT,SNTMP
      COMMON/SUMS19/SPX,SSFALL,SRM,SMELT,SMELTR,SROBG,DSFALL,DRAIN,
     1   DQNET,DRSL,NDRSP
      COMMON/SNUP19/MFC,SFALLX,WINDC,SCTOL,WETOL,SNOF,UADJC

CVK   CALCULATE AIR TEMPERATURE CHANGE, DTA
      DTA = TA - TPREV
      IF (SNDPT .LE. 0.0) THEN
         DS = 0.1
      ELSE
         DS = 0.1*WE/SNDPT
      ENDIF 
CVK-----------------------------------------------------

C     CONSTANTS
C     IF SNOWFALL EXCEEDS SNEW/HR--TINDEX=TPX
      SNEW=1.5
C     IF RAIN EXCEEDS RMIN/HR--USE RAIN-ON-SNOW MELT EQUATION
      RMIN=0.25
C     SBC=STEFAN/BOLTZMAN CONSTANT--MM/(((DEGK/100)**4)*HR)
      SBC=.0612
C.......................................
C.......................................
C     INITIAL VALUES
  100 ITPX=IDT/NDT
      FITPX=ITPX
      FNDT=NDT
      GM=PGM/FNDT
      SFNEW=SNEW*FITPX
      RFMIN=RMIN*FITPX
      SBCI=SBC*FITPX
      MC=0
      PSFALL=0.0
      PRAIN=0.0
      PQNET=0.0
      PSNWRO=0.0
      PROBG=0.0

CVK ----     V.I.K.  04/10/00  --------- 
      SXFALL=0.0
      SXMELT=0.0
      SXGSLOS=0.0
      SXRFRZ=0.0
C --------------------------------------     
C.......................................
C.......................................
C     CYCLE THROUGH THE COMPUTATIONAL PERIOD FOR EACH PRECIPITATION
C        TIME INTERVAL
      DO 200 I=1,NDT
      PXI=PX(I)
      IF((PXI.EQ.0.0).AND.(WE.EQ.0.0)) GO TO 160
      SFALL=0.0
      CNHSPX=0.0
      RAIN=0.0
      RAINM=0.0
      IF(PXI.EQ.0.0) GO TO 110
C.......................................
C     DETERMINE FORM OF PRECIP. AND ACCUMULATE SNOW COVER IF SNOW.
      PCT=PCTS(I)
      IF(PCT.GT.1.0) PCT=1.0
      TPX=TA
      IF(PCT.LT.0.0) GO TO 102
C
C     FORM OF PRECIP. INPUT.
      FRACS=PCT
      FRACR=1.0-PCT
      GO TO 105
  102 IF (LAEC.EQ.0) GO TO 104
      DRSL=DRSL+RSL
      NDRSP=NDRSP+1
C
C     FORM OF PRECIP. BASED ON RAIN-SNOW ELEVATION
      IF (RSL.GT.AE(1,1)) GO TO 10
      FRACR=0.0
      GO TO 30
   10 DO 20 J=2,NPTAE
      IF (RSL.GT.AE(1,J)) GO TO 20
      FRACR=AE(2,J-1)+(AE(2,J)-AE(2,J-1))*
     1  ((RSL-AE(1,J-1))/(AE(1,J)-AE(1,J-1)))
      GO TO 30
   20 CONTINUE
      FRACR=1.0
   30 FRACS=1.0-FRACR
      GO TO 105
C
C     FORM OF PRECIP. BASED ON TEMPERATURE.
  104 IF(TPX.GT.PXTEMP) GO TO 103
C
C     SNOW
      FRACS=1.0
      FRACR=0.0
      GO TO 105
C
C     RAIN
  103 FRACS=0.0
      FRACR=1.0
  105 IF(FRACS.EQ.0.0) GO TO 109
C
C     ACCUMULATE SNOWFALL
      TS=TPX
      IF(TS.GT.0.0) TS=0.0
      SFALL=PXI*FRACS*SCF
      IF(IFUT.EQ.0) SFALL=SFALL*SFALLX
      SPX=SPX+SFALL
      SSFALL=SSFALL+SFALL
      DSFALL=DSFALL+SFALL
      PSFALL=PSFALL+SFALL
      IF ((WE+LIQW).LT.SBWS) GO TO 106
      SBWS=SBWS+0.75*SFALL
      IF((SFALL.GE.SNOF).AND.(SB.GT.WE+LIQW)) SB=WE+LIQW
      GO TO 107
  106 IF(SFALL.GE.SNOF) SBWS=WE+LIQW+0.75*SFALL
  107 WE=WE+SFALL  
C     IF WE+LIQW.GE.3*SB, ASSUME NEW ACCUMULATION PERIOD
      IF(WE+LIQW.LT.3.0*SB) GO TO 108
      ACCMAX=WE+LIQW
      AEADJ=0.0
  108 CNHSPX=-TS*SFALL/160.0
      IF(SFALL.GT.SFNEW) TINDEX=TS
C
C     RAINFALL AND RAIN MELT.
  109 RAIN=PXI*FRACR
      SPX=SPX+RAIN
      PRAIN=PRAIN+RAIN
      IF(WE.EQ.0.0) GO TO 160
      DRAIN=DRAIN+RAIN
      TR=TPX
      IF(TR.LT.0.0) TR=0.0
      RAINM=0.0125*RAIN*TR
C.......................................
C     MELT AT GROUND-SNOW INTERFACE
  110 IF(WE.GT.GM) GO TO 111
      GMRO=WE+LIQW
      MELT=0.0
      ROBG=RAIN
      RAIN=0.0
      SROBG=SROBG+ROBG
      GO TO 150
  111 GMWLOS=(GM/WE)*LIQW
      GMSLOS=GM
C.......................................
C     COMPUTE SURFACE ENERGY EXCHANGE FOR THE COMPUTATIONAL PERIOD BASED
C        ON 100 PERCENT COVER AND NON-RAIN CONDITIONS -
      IF(MC.EQ.1) GO TO 115
C      PRINT *,"PACK19",MFMAX,MFMIN
      CALL MELT19(IDN,IMN,ALAT,TA,PMELT,MFMAX,MFMIN,MBASE,TINDEX,TIPM,
     1   PCNHS,NMF,LMFV,SMFV)
      MC=1
C.......................................
C     DETERMINE MELT FOR THE TIME INTERVAL - SURFACE ENERGY EXCHANGE
C        IS UNIFORM DURING THE COMPUTATIONAL PERIOD.
  115 CNHS=PCNHS/FNDT
      NR=1
      IF(RAIN.GT.RFMIN) GO TO 120
C
C     NON-RAIN OR LIGHT DIZZLE INTERVAL
      MELT=PMELT/FNDT
      MELT=MELT*MFC
      MELT=MELT+RAINM
      GO TO 130
C      print *,"norain",melt
C
C     RAIN INTERVAL.
  120 EA=2.7489E8*EXP(-4278.63/(TA+242.792))
C     ASSUME 90 PERCENT RELATIVE HUMIDITY DURING RAIN-ON-SNOW
      EA=0.90*EA
      TAK=(TA+273)*0.01
      TAK4=TAK*TAK*TAK*TAK
      QN=SBCI*(TAK4-55.55)
C
C     UADJC IS UADJ MOD MULTIPLIER added by mike smith 2/12/97
      
      QE=8.5*(EA-6.11)*UADJ*UADJC
        IF(IFUT.EQ.0) QE=QE*WINDC
      QH=7.5*0.000646*PA*UADJ*TA*UADJC
        IF(IFUT.EQ.0) QH=QH*WINDC
      MELT=QN+QE+QH+RAINM
      IF(MELT.LT.0.0) MELT=0.0
      NR=0
C        print *,"uadjc",melt
C.......................................
C     COMPUTE AREAL EXTENT OF SNOW COVER BASED ON CONDITIONS AT THE
C        BEGINNING OF THE TIME INTERVAL ADJUSTED FOR NEW SNOWFALL.
C
      
  130 CALL AESC19(WE,LIQW,ACCMAX,SB,SBAESC,SBWS,SI,ADC,AEADJ,AESC)
C      print *,'ASEC',we,liqw,accmax,sb,sbaesc,sbws,si,aeadj,aesc
C
C     ADJUST VALUES FOR AESC.
      IF(AESC.EQ.1.0) GO TO 134
      MELT=MELT*AESC
      CNHS=CNHS*AESC
      GMWLOS=GMWLOS*AESC
      GMSLOS=GMSLOS*AESC
C       print *,"aesc",melt
C.......................................
C     COMPUTE RAIN FALLING ON BARE GROUND
      ROBG=(1.0-AESC)*RAIN
      RAIN=RAIN-ROBG
      GO TO 135
  134 ROBG=0.0
C.......................................
C     COMPUTE SUM AND CHECK CNHS.
  135 SROBG=SROBG+ROBG
      IF((CNHS+NEGHS).LT.0.0) CNHS=-1.0*NEGHS

CVK  CUMULATE FOR TIME PERIOD
      SXFALL=SXFALL+SFALL
      SXMELT=SXMELT+MELT
      SXGSLOS=SXGSLOS+GMSLOS
CVK -------------------------

C.......................................
C     ADJUST WE FOR SURFACE AND GROUND MELT
C     GROUND MELT
      WE=WE-GMSLOS
      LIQW=LIQW-GMWLOS
      GMRO=GMSLOS+GMWLOS
C
C     SURFACE MELT
      IF(MELT.LE.0.0) GO TO 137
      IF(MELT.LT.WE) GO TO 136
C       print *,"surface melt",melt,we
      MELT=WE+LIQW
      QNET=MELT
      DQNET=DQNET+QNET
      IF(NR.EQ.1) SMELT=SMELT+MELT
      IF(NR.EQ.0) SMELTR=SMELTR+MELT
      GO TO 150
  136 WE=WE-MELT
C     QNET=NET SURFACE ENERGY EXCHANGE IN MILLIMETERS WE.
  137 QNET=MELT-CNHS-CNHSPX
      DQNET=DQNET+QNET
      PQNET=PQNET+QNET
      IF(NR.EQ.1) SMELT=SMELT+MELT
      IF(NR.EQ.0) SMELTR=SMELTR+MELT
C.......................................
C     PERFORM HEAT AND WATER BALANCE FOR THE SNOW COVER.
      WATER=MELT+RAIN
      HEAT=CNHS+CNHSPX
      LIQWMX=PLWHC*WE
      NEGHS=NEGHS+HEAT
C     TEMPERATURE OF SNOW CAN NOT BE BELOW-52.8 DEGC
      IF(NEGHS.LT.0.0) NEGHS=0.0
      IF(NEGHS.GT.0.33*WE) NEGHS=0.33*WE
      IF((WATER+LIQW).LT.(LIQWMX+NEGHS+PLWHC*NEGHS)) GO TO 140
C
C     EXCESS WATER EXISTS.
      EXCESS=WATER+LIQW-LIQWMX-NEGHS-PLWHC*NEGHS
      LIQW=LIQWMX+PLWHC*NEGHS
      WE=WE+NEGHS
      NEGHS=0.0
      GO TO 145
  140 IF(WATER.LT.NEGHS) GO TO 141
C
C     WATER EXCEEDS NEGHS - LIQUID WATER CONTENT IS INCREASED.
      LIQW=LIQW+WATER-NEGHS
      WE=WE+NEGHS

CVK  CUMULATE REFROZEN WATER
      SXRFRZ=SXRFRZ+NEGHS
      
      NEGHS=0.0
      EXCESS=0.0
      GO TO 145
C
C     ALL WATER IS REFROZEN IN THE SNOW COVER.
  141 WE=WE+WATER
      NEGHS=NEGHS-WATER
      EXCESS=0.0

CVK  CUMULATE REFROZEN WATER
      SXRFRZ=SXRFRZ+WATER
      
C     IF NO NEGATIVE HEAT - TINDEX MUST BE 0.0.
  145 IF(NEGHS.EQ.0.0) TINDEX=0.0
C.......................................
C     ROUTE EXCESS WATER THROUGH THE SNOW COVER.
      CALL ROUT19(ITPX,EXCESS,WE,AESC,STORGE,NEXLAG,EXLAG,PACKRO)
C.......................................
C     ADD GROUNDMELT RUNOFF TO SNOW COVER OUTFLOW.
      PACKRO=PACKRO+GMRO
      GO TO 190
C.......................................
C     SNOW GONE - SET ALL CARRYOVER TO NO SNOW CONDITIONS.
  150 TEX=0.0
      DO 151 N=1,NEXLAG
  151 TEX=TEX+EXLAG(N)
      PACKRO=GMRO+MELT+TEX+STORGE+RAIN
      CALL ZERO19
      AESC=0.0
      GO TO 190
C.......................................
C     NO SNOW COVER - NO NEW SNOWFALL.
  160 ROBG=PXI
      PACKRO=0.0
      AESC=0.0
      SROBG=SROBG+ROBG
C.......................................
C     COMPUTE RAIN+MELT
  190 RM(I)=PACKRO+ROBG
      SRM=SRM+RM(I)
      PSNWRO=PSNWRO+PACKRO
      PROBG=PROBG+ROBG
  200 CONTINUE
C     END OF COMPUTATIONAL PERIOD
C.......................................
C.......................................
C     SET SIMULATED AESC AND TOTAL WATER-EQUIVALENT.
      TEX=0.0
      DO 210 N=1,NEXLAG
  210 TEX=TEX+EXLAG(N)

CVK --  V.KOREN  04/05/00   ---------------------------------
CVK   CALL SNDEPTH SUBROUTINE TO CALCULATE SNOW DEPTH
      IF(WE .GT. 0.) THEN
       SLIQ=LIQW+TEX+STORGE
       SXFALL=SXFALL-SXMELT
       IF(SXFALL .LT. 0.) SXFALL=0.0
       CALL SNDEPTH(WE,SLIQ,SXFALL,SXGSLOS,SXRFRZ,
     +              TA,DTA,IDT,SNDPT,DS,SNTMP)
      ELSE
       SNDPT=0.
       SNTMP=0.
       DS=0.1
      ENDIF
CVK ---------------------------------------------------------      
C      print *,"endofpack",melt,packro
      TWE=WE+LIQW+TEX+STORGE
      IF (TWE.EQ.0.0) GO TO 215
C
C     COMPUTE AREAL EXTENT BASED ON CONDITIONS AT THE END OF THE PERIOD.
      CALL AESC19(WE,LIQW,ACCMAX,SB,SBAESC,SBWS,SI,ADC,AEADJ,AESC)
  215 COVER=AESC

C
C     STORE VALUES SO COMPUTED VALUES WILL BE AVAILABLE FOR PRINTOUT
C        EVEN IF UPDATING OCCURS.
      CWE=TWE
      CAESC=COVER
C.......................................
C     UPDATING SECTION
      IF((OWE.LT.0.0).AND.(OSC.LT.0.0)) GO TO 280
      CALL UPDT19(OWE,OSC,TWE,COVER,IUPWE,IUPSC,WETOL,SCTOL,
     1  SI,ADC)

  280 CONTINUE

C.......................................
      RETURN
      END

